; put "irq"
;
;*********************
;
; all the irq routines
;
;*********************
;
irqmsk=ted+10
rascom=ted+11
irqreg=ted+9
;
;
;
rasval
;
.byte $17,$41,$b0,$c0,$cc
;
setirq
;
lda $0314
cmp #<serv
bne stirq
;
rts
;
stirq
;
sei
;
lda #<serv
sta $0314
lda #>serv
sta $0315
;
lda #%00000010
sta irqmsk
;
; enable irq's from :-
;
; raster
;
; no hassle from timer 2 in this !
;
lda rasval
sta rascom
;
lda #0
sta intype
;
; set raster compare
;
cli
;
rts
;
;
;
;
serv
;
;************************
;
;irq comes here - wow !!!
;
;************************
;
;
lda irqreg
sta irqreg ;* reset bits
;
ldx intype ;* raster number
;
inx
cpx #5
bne nolast
;
ldx #0
;
nolast
;
stx intype
;
;
;*************************
;
; raster service routine
;
;*************************
;
lda intype
bne noras1
;
jmp rast1
;
noras1
;
cmp #1
bne noras2
;
jmp rast2
;
noras2
;
cmp #2
bne noras3
;
jmp rast3
;
noras3
;
cmp #3
bne noras4
;
jmp rast4
;
noras4
;
cmp #4
bne noras5
;
jmp rast5
;
noras5
;
;
;
;
;
;
;
;
;
;************
;
;  irq exit
;
;************
;
irqend
;
ldx intype
lda rasval,x
sta rascom
;
pla
tay
pla
tax
pla
rti
;
;
;
;
;
.end 
